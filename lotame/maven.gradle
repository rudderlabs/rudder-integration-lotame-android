
apply plugin: 'maven'
apply plugin: 'signing'

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

signing {
    sign configurations.archives
}

group = 'com.rudderstack.android.integration'
archivesBaseName = 'lotame'
version = '1.0.2'

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    new FileInputStream(secretPropsFile).withCloseable { is ->
        p.load(is)
    }
    p.each { name, value ->
        ext[name] = value
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: 'https://s01.oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Rudder-Integration-Lotame-Android'
                artifactId 'lotame'
                packaging 'aar'
                description 'RudderStack\'s Native SDK Integration support for Lotame.'
                url 'https://github.com/rudderlabs/rudder-integration-lotame-android'

                scm {
                    connection 'scm:git:git://github.com/rudderlabs/rudder-integration-lotame-android.git'
                    developerConnection 'scm:git:ssh://github.com:rudderlabs/rudder-integration-lotame-android.git'
                    url 'https://github.com/rudderlabs/rudder-integration-lotame-android/tree/master'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://github.com/rudderlabs/rudder-integration-lotame-android/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'desusai7'
                        name 'Desu Sai Venkat'
                        email 'venkat@rudderstack.com'
                    }
                }
            }
        }
    }
}